plugins {
  id 'java'
  id 'java-library'
  id 'jacoco'
  id 'maven'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.4'
  id 'org.sonarqube' version '2.6.2'
}

group = 'com.amdelamar'
version = '2.1.0'
description = 'Password hashing utility in Java. It can hash passwords with PBKDF2 hmac SHA1/SHA256/SHA512, BCRYPT, or SCRYPT, and it salts automatically and has a pepper option.'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
  jcenter()
}

dependencies {
  implementation 'commons-codec:commons-codec:1.8'
  testCompile 'junit:junit:4.12'
}

jar {
  baseName = 'jhash'
  from sourceSets.main.allSource
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compile
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = "javadoc"
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled false
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      artifact (sourcesJar) {
        classifier = "sources"
      }
      artifact (javadocJar) {
        classifier = "javadoc"
      }
      version this.version
      groupId this.group
      artifactId 'jhash'
    }
  }
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
  publications = ['MyPublication']
  publish = true
  override = true
  pkg {
    repo = 'mvn'
    name = 'jhash'
    desc = this.description
    licenses = ['Apache-2.0']
    websiteUrl = 'https://amdelamar.com/jhash/'
    issueTrackerUrl = 'https://github.com/amdelamar/jhash/issues'
    vcsUrl = 'git://github.com/amdelamar/jhash.git'
    version {
        name = this.version
        desc = this.description
        released  = new Date()
    }
    githubRepo = 'amdelamar/jhash' // Optional Github repository
    githubReleaseNotesFile = 'README.md' // Optional Github release notes
    labels = ['password', 'hash', 'pbkdf2', 'hmac' ,'sha' ,'sha256' ,'bcrypt' ,'scrypt']
    publicDownloadNumbers = true
  }
}
